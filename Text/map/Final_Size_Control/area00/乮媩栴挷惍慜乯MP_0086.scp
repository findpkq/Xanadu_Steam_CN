
// ----------------------------------------------------------------------------
// 初期状態                                                                    
// ----------------------------------------------------------------------------

DEF("MAP")

RES("INIT")
{
	SET_COLOR("d3301",0x7f000000,1,0)

	// カギ設定
	CHECK_GAME_FLAG(3301)
	ON_SKIP(2)
	KAGI("d3301",1)
	SKIP(1)
	INIT_SWITCH_ON("d3301")

	SWITCH_ON("fire1")
	SWITCH_ON("fire2")
	SWITCH_ON("fire3")
	SWITCH_ON("fire4")

	// レバーフラグ
	CHECK_GAME_FLAG(3302)
	OFF_SKIP(3)
	HIDE_CHR("eye")
	HIDE_CHR("kagi")
	LOAD_MAP("MP_008e")

}


// ----------------------------------------------------------------------------
// カギ開け                                                                    
// ----------------------------------------------------------------------------

DEF("d3301")
RES("KAGI")
{
	GAME_FLAG_ON(3301)
}


// ----------------------------------------------------------------------------
// 目玉                                                                        
// ----------------------------------------------------------------------------

DEF("eye")

RES("HIT")
{
	GAME_FLAG_ON(3302)

	// EV C-3 奇岩城出現
	game_flag_on(3702)

	STOP_PC(0)
	CINEMA_ON(0)
	pick_item(191)

	//直撃のフラッシュピカーーーッ！
	FADE_OUT_EX(10, 0, "0xffffffff", 1)
	WAIT(10)
	FADE_IN_EX(5, 0, "0xffffffff", 1)
	WAIT(2)

	//死亡エフェクト発動
	ATTACH_EFC("EFC_00BREAKEYE","obj","CTR")
	WAIT(100)
	
	//フラッシュチカチカチカ・・・！！
	FADE_OUT_EX(5, 0, "0xffffffff", 1)
	WAIT(5)
	FADE_IN_EX(4, 0, "0xffffffff", 1)
	WAIT(4)
	FADE_OUT_EX(5, 0, "0xffffffff", 1)
	WAIT(5)
	FADE_IN_EX(4, 0, "0xffffffff", 1)
	WAIT(4)

	WAIT(40)

	//ホワイトアウト。ムービーへ繋げる
	FADE_OUT_EX(200, 0, "0xffffffff", 1)
	KILL_CHR("eye",0,1,220)
	KILL_CHR("kagi",0,1,220)
	BLUR(100)
	WAIT(200)
	BLUR(0)

	//水なしマップと挿げ替え
	LOAD_MAP("MP_008e")
	
	//通常に戻す
	FADE_IN_EX(90, 0, "0xffffffff", 1)
	WAIT(90)
	CINEMA_OFF(0)
}



// ----------------------------------------------------------------------------
// 碑文
// ----------------------------------------------------------------------------

DEF("Plate")
RES("CLICK")
{
	FOCUS_CHR("Plate")
		SET_NAME("Plate","碑文")
		SYS("血を分けし凛然たる姫も、|虚栄なる我が伴侶の象徴も、|悲しみとともに昏き淵の底に沈めん|　|　　　　　　　黒き花嫁と呼ばれし愚者")
}

